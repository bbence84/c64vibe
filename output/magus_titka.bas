10 POKE 53280,4:POKE 53281,0:POKE 646,13:PRINT CHR$(147)
20 S=54272:POKE S+24,15:X=RND(-TI)
30 N=INT(RND(1)*100)+1:C=0
40 PRINT CHR$(147):PRINT TAB(9)"*** A MAGUS TITKA ***"
50 PRINT:PRINT "GONDOLTAM EGY SZAMRA 1 ES 100 KOZOTT."
60 PRINT:INPUT "MI A TIPPED";T:C=C+1
70 IF T=N THEN GOTO 120
80 IF T>N THEN PRINT "TUL MAGAS!":GOSUB 1000:GOTO 60
90 IF T<N THEN PRINT "TUL ALACSONY!":GOSUB 1000:GOTO 60
100 GOTO 60
110 REM WINNER
120 PRINT "ELTALALTAD! GRATULALOK!":GOSUB 2000
130 PRINT "PROBALKOZASOK SZAMA: "C
140 INPUT "JATEK UJRA? (I/N)";A$
150 IF A$="I" THEN GOTO 30
160 IF A$="N" THEN PRINT "VISZLAT!":END
170 GOTO 140
1000 REM SOUND BUZZ
1010 POKE S+5,0:POKE S+6,240:POKE S,0:POKE S+1,10
1020 POKE S+4,33:FOR D=1 TO 50:NEXT:POKE S+4,32:RETURN
2000 REM SOUND VICTORY
2010 POKE S+5,9:POKE S+6,0
2020 FOR K=25 TO 50 STEP 5
2030 POKE S,0:POKE S+1,K:POKE S+4,17
2040 FOR D=1 TO 30:NEXT:POKE S+4,16
2050 NEXT K:RETURN
10 REM BOWL AND SCORE BY ROB VONK 2020
20 REM BASED ON DICE GAME OF SAME NAME BY E.S. LOWE, 1962
30 REM SCORING ROUTINE BY PAUL DUNN
40 PRINT"":POKE 53280,0:POKE 53281,0:POKE 646,1
50 PRINT"THIS GAME IS CALLED 'BOWL AND SCORE'."
60 PRINT:PRINT"THIS GAME CONSISTS OF TEN REGULAR DICE"
70 PRINT"WITH A PICTURE OF A BOWLING PIN ON ONE"
80 PRINT"SIDE (DENOTED BY 'O') AND BLANKS ON THE"
90 PRINT"OTHER FIVE (DENOTED BY '-'). THE PLAYER"
100 PRINT"ROLLS ALL THE DICE. ANY BLANKS ARE SET"
110 PRINT"ASIDE AND REPRESENT THE NUMBER OF PINS"
120 PRINT"KNOCKED DOWN WITH THE FIRST BALL. THE"
130 PRINT"REMAINING DICE ARE ROLLED AND SCORED"
140 PRINT"THE SAME WAY. THIS REPRESENTS ONE"
150 PRINT"FRAME. THIS CONTINUES FOR TEN FRAMES."
160 PRINT"SCORE IS FIGURED JUST LIKE REGULAR"
170 PRINT"BOWLING."
180 PRINT:PRINT"HIT A KEY"
190 GET I$:IF I$<>"" THEN 190
200 GET I$:IF I$="" THEN 200
210 PRINT"":DIM P(10),S(21):POKE 53280,0:POKE 53281,0:POKE 646,1:Z=RND(-TI)
220 FOR I=1 TO 21:S(I)=0:NEXT I
230 FOR F=1 TO 10:B=1:S=0:GOSUB 460:GOSUB 490
240 GOSUB 610:PRINT:PRINT TAB(8);"FRAME";F;"BALL";B;"SCORE";SC
250 PRINT:PRINT TAB(11);"HIT A KEY TO BOWL"
260 GET I$:IF I$<>"" THEN 260
270 GET I$:IF I$="" THEN 270
280 GOSUB 580:GOSUB 480:S=S+P:S(F*2-2+B)=P:IF F=10 THEN 330
290 IF B=2 OR (S=10 AND B=1) THEN 410
300 B=B+1:GOTO 240
310 REM BONUS BALL ROUTINE FOR 10TH FRAME
320 REM IF STRIKE ON FIRST BALL, TWO MORE BALLS
330 IF B=1 AND S=10 THEN PRINT TAB(13);"BOWL TWO MORE!"
340 IF B=1 AND S=10 THEN S=0:GOSUB 460:GOSUB 480:GOTO 300
350 REM IF STRIKE OR SPARE ON SECOND BALL, ONE MORE BALL
360 IF B=2 AND S(19)=10 AND S(20)<10 THEN PRINT TAB(13);"BOWL ONE MORE!":S=0
370 IF B=2 AND S(19)=10 AND S(20)<10 THEN 300
380 IF B=2 AND (S(19)=10 OR S(19)+S(20)=10) THEN PRINT TAB(13);"BOWL ONE MORE!"
390 IF B=2 AND (S(19)=10 OR S(19)+S(20)=10) THEN S=0:GOSUB 460:GOSUB 480:GOTO 300
400 IF B=1 THEN 300
410 NEXT F:GOSUB 610:PRINT:PRINT TAB(9);"YOUR FINAL SCORE IS";SC
420 PRINT:PRINT"ANOTHER GAME (Y/N)";
430 INPUT I$:IF I$<>"Y" AND I$<>"N" THEN PRINT"Y OR N PLEASE.":GOTO 430
440 IF I$="Y" THEN 220
450 END
460 REM SET UP PINS
470 FOR I=1 TO 10:P(I)=1:NEXT I:RETURN
480 REM DISPLAY PINS
490 PRINT:PRINT TAB(17);:FOR I=7 TO 10:IF P(I)=1 THEN PRINT"O ";:GOTO 510
500 PRINT"- ";
510 NEXT I:PRINT:PRINT TAB(18);:FOR I=4 TO 6:IF P(I)=1 THEN PRINT"O ";:GOTO 530
520 PRINT"- ";
530 NEXT I:PRINT:PRINT TAB(19);:FOR I=2 TO 3:IF P(I)=1 THEN PRINT"O ";:GOTO 550
540 PRINT"- ";
550 NEXT I:PRINT:PRINT TAB(20);:IF P(1)=1 THEN PRINT"O":GOTO 570
560 PRINT"-"
570 PRINT:RETURN
580 REM BOWL BALL
590 P=0:FOR I=1 TO 10:IF INT(RND(1)*6+1)<6 AND P(I)=1 THEN P(I)=0:P=P+1
600 NEXT I:RETURN
610 REM SCORING CALCULATOR
620 SC=0:FOR I=1 TO 17 STEP 2:SC=SC+S(I)+S(I+1)
630 IF S(I)=10 THEN SC=SC+S(I+2)+S(I+3)+(S(I+4)*-(S(I+3)=0)):GOTO 660
640 REM REMOVE '-' IF TRUE IS 1 ON YOUR COMPUTER INSTEAD OF -1
650 IF S(I)+S(I+1)=10 THEN SC=SC+S(I+2)
660 NEXT I:SC=SC+S(19)+S(20)+S(21):RETURN
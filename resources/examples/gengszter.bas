5 REM C= UJSAG SORSZAM 031
6 REM" GENGSZTER-KALANDJATEK
7 REM" IRTA TIHOR MIKLOS 1986-BAN
8 REM
10 POKE53272,21:POKE650,128:POKE53280,0:POKE53281,0:POKE646,1
20 X=RND(-TI):PRINTCHR$(8)
30 DIMEG$(3),H%(11,3),I$(26),T$(8),TX(8),F$(11),FF%(11),Z%(14):IG%=26
40 FORJ=0TO3:READEG$(J):NEXTJ:FORI=1TO11:FORJ=0TO3:READH%(I,J):NEXTJ,I
50 FORI=1TOIG%:READI$(I):NEXTI:FORI=1TO8:READT$(I):TX(I)=255:NEXTI
60 FORI=1TO11:READF$(I):NEXTI:FORI=1TO11:READFF%(I):NEXTI
65 GOSUB 4000
70 HE%=1:FORJ=1TO25:PRINT:NEXTJ
80 ONHE%GOSUB1300,1350,1380,1390,1420,1430,1440,1450,1460,1490,1520
90 GOSUB1530
100 C=0:FORJ=1TO8:IFT%(J)<>HE%THEN130
110 IFC=0THENC=1:PRINT"EZT LATOD:"
120 PRINTSPC(10)T$(J)
130 NEXTJ
140 IFHE%=3THEN2380
150 IFHE%=5THEN2410
160 IFHE%=11THEN2670
170 IFHE%=8ANDZ%(14)<5THENZ%(14)=Z%(14)+1:PRINT"A PLAFON GYORS IRAMBAN SULLYED."
180 IFZ%(14)=4THEN2540
190 PRINT:INPUT"MIT CSINALSZ";M$:PRINT:IFM$=""THEN170
200 REM" --- UTASITAS KIERTEKELESE ---
210 M$=M$+" ":U%=0:TT%=0:FX=0
220 C$="":FORK=1TOLEN(M$)
230 B$=MID$(M$,K,1):IFB$=" "THEN260
240 C$=C$+B$
250 NEXTK:GOTO360
260 IFC$=""THEN250
270 IFC$="TOLGYAJTO"ORC$="FAAJTO"THENC$="AJTO"
280 C=0:FORI=1TOIG%:IFC$=I$(I)THENU%=I:C=1
290 NEXTI:IFC=1THEN350
300 FORI=1TO8:IFC$=T$(I)THENTT%=I:C=1
310 NEXTI:IFC=1THEN350
320 FORI=1TO11:IFC$=F$(I)THENFX=I:C=1
330 NEXTI:IFC=1THEN350
340 PRINT"NEM ERTEM EZT A SZOT "CHR$(34)C$CHR$(34):GOTO170
350 C$="":GOTO250
360 REM" --- UTASITAS VEGREHAJTASA ---
370 IFU%=0THENPRINT"TESSEK?":GOTO170
380 IFU%>4THEN430
390 REM" --- MOZGAS ---
400 IFH%(HE%,U%-1)=0THENPRINT"NEM MEHETSZ ARRA!":GOTO170
410 IFHE%=8ANDZ%(14)<5THEN2450
420 PRINTEG$(U%-1)" MENTEL.":HE%=H%(HE%,U%-1):FORJ=1TO50:NEXTJ:GOTO80
430 ONU%-4GOTO450,530,630,570,570,450,530,980,1590,630,660,1660,1720,1780,1780
440 ONU%-19GOTO1830,1880,2020,2090,2190,2220,2350
450 REM" --- TARGY FELVETELE ---
460 IFFX>0THENPRINT"TUL NEHEZ!":GOTO170
470 IFTT%=0THENPRINT"MIT?":GOTO170
480 IFTX(TT%)<>0THENPRINT"MAR NALAD VAN!":GOTO170
490 IFTT%=8ANDTX(8)=255ANDHE%=7THEN520
500 IFTX(TT%)<>HE%THENPRINT"NEM LATOM AZT A TARGYAT!":GOTO170
510 TX(TT%)=0:PRINT"OK.":GOTO170
520 PRINT"CSAK UGY EGYSZERUEN NEM LEHET FELVENNI.":GOTO170
530 REM" --- TARGY LERAKASA ---
540 IFTT%=0THENPRINT"MIT?":GOTO170
550 IFTX(TT%)<>0THENPRINT"NINCS NALAD AZ A TARGY!":GOTO170
560 TX(TT%)=HE%:PRINT"OK.":GOTO170
570 REM" --- LISTA ---
580 C=0:FORI=1TO8:IFTX(I)<>0THEN610
590 IFC=0THENC=1:PRINT"NALAD VAN:"
600 PRINTSPC(10)T$(I)
610 NEXTI:IFC=0THENPRINT"NINCS NALAD SEMMI."
620 GOTO170
630 REM" --- NEZ ---
640 IFTT%=0ANDFX=0THEN80
650 GOTO680
660 REM" --- VIZSGAL ---
670 IFTT%=0ANDFX=0THENPRINT"MIT?":GOTO170
680 IFFX>0THEN800
690 IFTX(TT%)<>0THENPRINT"NINCS NALAD AZ A TARGY!":GOTO170
700 ONTT%GOTO710,720,710,710,710,710,790,790
710 PRINT"SEMMI FONTOSAT NEM LATSZ RAJTA.":GOTO170
720 PRINT"EZ OLVASHATO A LEVELBEN:"
730 PRINT" A GENGSZTERFONOK ELFOGOTT ES BEZART"
740 PRINT" HA ELEG UGYES VAGY IDEJEBEN KISZABADUL-"
750 PRINT" NI, VEGEZ VELED! IGAZI KALANDJATEKOS-"
760 PRINT" NAK VALO FELADAT!"
770 GOTO170
780 PRINT"SZAMITOGEPBE LEHET BERAKNI.":GOTO170
790 PRINT"IGEN EROSNEK TUNIK.":GOTO170
800 IFFX=4THEN710
810 IFFX=10THEN80
820 IFFF%(FX)<>HE%THENPRINT"NEM LATOM AZT A DOLGOT.":GOTO170
830 ONFXGOTO840,860,710,710,890,710,920,940,960,710,710
840 IFZ%(11)=1THEN710
850 Z%(11)=1:PRINT"EGY DARAB DROTOT TALALTAL!":TX(1)=1:GOTO170
860 IFZ%(6)=0THEN880
870 PRINT"MAR NYITVA VAN.":GOTO170
880 PRINT"ZARVA VAN!":GOTO170
890 IFTX(2)<255THEN710
900 PRINT"AZ ASZTALNAK VAN EGY FIOKJA. AZ ASZTA- LON EGY LEVELET LATSZ."
910 TX(2)=1:GOTO170
920 IFZ%(9)=0THENPRINT"NINCS BEKAPCSOLVA.":GOTO170
930 PRINT"BE VAN KAPCSOLVA.":GOTO170
940 IFTX(6)<255THEN710
950 PRINT"VAN ITT EGY SZEKRENYKULCS.":TX(6)=10:GOTO170
960 IFZ%(7)=0THENPRINT"CSUKVA VAN!":GOTO170
970 GOTO870
980 REM" --- NYIT ---
990 IFTT%=0ANDFX=0THEN1010
1000 GOTO1020
1010 PRINT"NEM TEHETED MEG!":GOTO170
1020 IFTT%<>0ANDFX=0THENPRINT"MIT?":GOTO170
1030 IFFX=4THEN1190
1040 IFFX=10THEN1010
1050 IFFF%(FX)<>HE%THEN820
1060 ONFXGOTO1010,1070,1110,1030,1010,1150,1160,1010,1170,1010,1010
1070 IFZ%(6)=1THEN870
1080 IFTX(6)>0THEN1100
1090 PRINT"OK. EGY KODKARTYAT TALALTAL.":TX(7)=1:Z%(6)=1:W%=W%+10:GOTO170
1100 PRINT "NINCS HOZZA KULCSOD!":GOTO170
1110 IFZ%(3)=1THEN870
1120 IFTX(4)>0THEN1100
1130 Z%(1)=1:H%(1,0)=2
1140 PRINT"OK.":W%=W%+10:GOTO170
1150 PRINT"A FIOKOT NEM NYITNI KELL, HANEM...":GOTO170
1160 PRINT"EZT BIZZUK INKABB A SZERVIZRE.":GOTO170
1170 IFZ%(7)=1THEN870
1180 PRINT"OK. EGY BENZINESKANNAT TALALTAL.":TX(5)=6:Z%(7)=1:GOTO170
1190 ONHE%GOTO1210,1230,1200,1250,1200,1200,1200,1200,1270,1200,1200
1200 PRINT"NEM LATOK CSUKOTT AJTOT.":GOTO170
1210 IFZ%(4)=1THEN870
1220 GOTO1100
1230 IFZ%(2)=1THEN870
1240 GOTO1150
1250 IFZ%(3)=1THEN870
1260 GOTO1100
1270 IFZ%(5)=1THEN870
1280 PRINT"EZT SAJNOS NEM IS LEHET KINYITNI.":GOTO170
1290 REM" --- HELYSZINEK SZOVEGE
1300 PRINT"EGY IRODABAN VAGY. VAN ITT EGY FOTEL,"
1310 PRINT"EGY SZEKRENY, EGY ASZTAL,";
1320 IFZ%(1)=0THENPRINT" EGY ZART VAS- AJTO ESZAKRA,";
1330 IFZ%(4)=0THENPRINT" EGY ZART TOLGYAJTO DELRE,";
1340 PRINTCHR$(20)",":RETURN
1350 PRINT"EGY ELOSZOBABAN VAGY."
1360 IFZ%(2)=0THENPRINT"VAN ITT EGY ZART AJTO KELETRE."
1370 RETURN
1380 PRINT"EGY HOMALYOS HELYISEGBEN VAGY.":RETURN
1390 PRINT"EGY NAGY, FEHER SZOBABAN VAGY."
1400 IFZ%(3)=0THENPRINT"VAN ITT EGY FAAJTO KELETRE."
1410 RETURN
1420 PRINT"EGY IZLESESEN BERENDEZETT SZOBABAN VAGY.":RETURN
1430 PRINT"EGY RAKTARBAN VAGY. VAN ITT EGY UVEG- SZEKRENY.":RETURN
1440 PRINT"EGY FOLYOSON VAGY. A MENNYEZETET NEHANY GERENDA TAMASZTJA ALA.":RETURN
1450 PRINT"EGY HATALMAS TEREMBEN VAGY.":RETURN
1460 PRINT"EGY BUTORTALAN, SZUK HELYISEGBEN VAGY."
1470 IFZ%(5)=0THENPRINT"VAN ITT EGY ELEKTRONIKUSAN VEZERELT     AJTO."
1480 RETURN
1490 PRINT"A VEZERLOTEREMBEN VAGY. EGY POLCOT ES  EGY SZAMITOGEPET LATSZ."
1500 IFZ%(9)=0THENPRINT"A SZAMITOGEP KEPERNYOJE FEKETEN ASIT    RAD."
1510 RETURN
1520 PRINT"AZ UTCAN VAGY.":RETURN
1530 REM" --- KIJARATOK ---
1540 C=0:FORJ=0TO3:IFH%(HE%,J)=0THEN1570
1550 IFC=0THENC=1:PRINT"NYITOTT AJTOT LATSZ:"
1560 PRINT" "EG$(J)",";
1570 NEXTJ:IFC=0THENPRINT"NEM LATSZ KIJARATOT.":RETURN
1580 PRINTCHR$(20)".":RETURN
1590 REM" --- HUZ ---
1600 IFTT%=0ANDFX=0THENPRINT"MIT?":GOTO170
1610 IFFX<>6THEN1650
1620 IFHE%<>1THEN820
1630 IFZ%(8)=1THEN870
1640 PRINT"OK. EGY DOBOZ GYUFAT ES EGY VASKULCSOT  TALALTAL.":TX(3)=1:TX(4)=1
1645 Z%(8)=1:GOTO170
1650 REM" --- RUG ---
1660 IFTT%=0ANDFX=0THENPRINT"MIT?":GOTO170
1670 IFFX<>4THENPRINT"VANDAL!":GOTO170
1680 IFHE%<>4THEN2500
1690 IFZ%(3)=1THEN870
1710 PRINT"RECCS-ROPP! NYITVA AZ AJTO!":Z%(3)=1:H%(4,2)=6:W%=W%+10:GOTO170
1720 REM" --- BEKAPCSOL ---
1730 IFFX=0ANDTT%=0THENPRINT"MIT?":GOTO170
1740 IFFX<>7THENPRINT"NE VICCELJ!":GOTO170
1750 IFHE%<>10THEN820
1760 IFZ%(9)=1THENPRINT"MAR BEKAPCSOLTAD.":GOTO170
1770 Z%(9)=1:PRINT"OK. A SZAMITOGEP A KODKARTYAT KERI.":W%=W%+10:GOTO170
1780 REM" --- LOT ---
1790 IFFX=0ANDTT%=0THENPRINT"MIT?":GOTO170
1800 IFTT%<>8THEN1810
1810 IFHE%<>7ORZ%(10)=1THEN1010
1820 PRINT"OK. MOST MAR A KEZEDBE VEHETED!":Z%(10)=1:TX(8)=7:W%=W%+10:GOTO170
1830 REM" --- ONT ---
1840 IFFX=0ANDTT%=0THENPRINT"MIT?":GOTO170
1850 IFTT%<>5ANDFX<>11THENPRINT"NEM FOLYEKONY.":GOTO170
1860 IFTX(5)<>6THEN550
1870 PRINT"KIONTOTTED A BENZINT.":TX(5)=-HE%:W%=W%+10:GOTO170
1880 REM" --- GYUJT ---
1890 IFTT%=0ANDFX=0THENPRINT"MIT?":GOTO170
1900 IFTT%<>3THEN1930
1910 IFFX=11THEN1960
1920 PRINT"KIS PIROMANIAS!":GOTO170
1930 IFTX(3)>0THEN690
1940 IFZ%(13)=1THENPRINT"MAR EG.":GOTO170
1950 PRINT"OK.":Z%(13)=1:GOTO170
1960 IFTX(3)>0ORZ%(13)=0THENPRINT"NINCS EGO GYUFAD.":GOTO170
1970 IFTX(5)>0THEN2530
1980 IFTX(5)<>-HE%THENPRINT"NEM LATOK KIONTOTT BENZINT.":GOTO170
1990 PRINT"OK.":FORJ=1TO100:NEXTJ
2000 IFHE%=1THENPRINT"A TOLGYAJTO SZENNE EGETT.":H%(1,1)=10:W%=W%+10
2010 TX(5)=250:TX(3)=250:PRINT"A TUZ ELALUDT.":GOTO170
2020 REM" --- TAMASZT ---
2030 IFFX=0ANDTT%=0THENPRINT"MIT?":GOTO170
2040 IFTT%<>8THEN1010
2050 IFZ%(8)>0THEN690
2060 IFHE%<>8ORZ%(14)=5THENPRINT"NEM LATOM ERTELMET.":GOTO170
2070 PRINT"SIKERULT A GERENDAVAL KITAMASZTANI A  SULLYEDO PLAFONT!"
2080 W%=W%+10:TX(8)=250:Z%(14)=5:GOTO170
2090 REM" --- HASZNAL ---
2100 IFTT%=0THENPRINT"MIT?":GOTO170
2110 IFTX(TT%)>0THEN690
2120 ONTT%GOTO2160,2130,2170,2170,2170,2170,2180,2060
2130 PRINT"NEM TUDOD SEMMIRE SEM HASZNALNI.":GOTO170
2140 IFHE%<>2ORZ%(2)=1THEN2130
2150 PRINT"A DROTTAL KINYITOTTAD AZ AJTO ZARJAT!":Z%(2)=1:H%(2,2)=4
2160 W%=W%+10:GOTO170
2170 PRINT"HOGYAN?":GOTO170
2180 PRINT"HOGYAN? (PROBALD VIZSGALNI!)":GOTO170
2190 REM" --- OLVAS ---
2200 IFTT%<>2THENPRINT"NINCSENEK RAJTA BETUK.":GOTO170
2210 GOTO660
2220 REM" --- BERAK ---
2230 IFTT%=0THENPRINT"MIT?":GOTO170
2240 IFTT%<>7THENPRINT"NA NE VICCELJ!":GOTO170
2250 IFTX(7)>0THEN690
2260 IFHE%<>10THENPRINT"VAJON HOVA?":GOTO170
2270 IFZ%(9)=0THENPRINT"NINCS BEKAPCSOLVA A SZAMITOGEP.":GOTO170
2280 PRINT"OK.":PRINT"IRTA A SZAMITOGEPEN A KOVETKEZO SZAMSOR JELE-NIK MEG:";
2290 A=INT(RND(1)*4+2):B=INT(RND(1)*4+2):S=INT(RND(1)*6+4):PRINT S;CHR$(20)"=";
2300 FORJ=1TO2:S=S*A+B:PRINT S;CHR$(20)",";:NEXTJ:PRINT S;CHR$(20):S=S*A+B
2310 INPUT"MILYEN SZAMOT IRSZ BE A GEPNEK";Q
2320 IFQ<>STHEN2470
2330 PRINT"A SZAMITOGEPEN A KOVETKEZO FELIRAT JEL- ENIK MEG: 'RENDBEN, NYITVA'"
2340 W%=W%+10:Z%(5)=1:TX(7)=250:H%(9,0)=11:GOTO170
2350 REM" --- VAR ---
2360 PRINT"VARSZ. AZ IDO TELIK...":GOTO170
2370 REM" --- HALALNEMEK ---
2380 PRINT"EGY NYAJAS FEKETEPARDUC SETAL FELED A"
2390 PRINT"A SZOBAN AT. REMULETEDBEN MOZDULNI SEM"
2400 PRINT"TUDSZ. AZTAN A PARDUC ELRUGASZKODIK...":GOTO2560
2410 PRINT"A GENGSZTERFONOK VEZERKARAVAL ITT VAR"
2420 PRINT"RAD. - EGY KIS EGESZSEGUGYI SETA? -"
2430 PRINT"KERDEZI. A KEZEBEN EGY PISZTOLY VILLAN."
2440 PRINT"EGY DORRENEST HALLASZ...":GOTO2560
2450 PRINT"NEM SIKERULT AZ AJTOT ELERNED. TESTEDRE"
2460 PRINT"EGYRE EROSEBB NYOMAS NEHEZEDIK...":GOTO2560
2470 PRINT"VALAHOL A HAZ MASIK VEGEBEN EGY SZIRENA"
2480 PRINT"KEZD VIJJOGNI. AZTAN A SZEKED KARFAJABOL";
2490 PRINT "KEKES SZIKRAK PATTANNAK KI. ARAMUTEST    ERZEL...":GOTO2560
2500 PRINT "DUHODTEN RUGDALOD AZ AJTOT, DE HIABA."
2510 PRINT "KESOBB VESZED ESZRE, HOGY VALAKI A HATADBA"
2520 PRINT "KERUL. EGY UTEST ERZEL...":GOTO2560
2530 PRINT "HATALMAS ROBBANAS...":GOTO2560
2540 PRINT "MEG ERZED MAGADON AZ IRDATLAN RAD NE-"
2550 PRINT "HEZEDO NYOMAST. AZTAN RECCSEN A KOPO- NYAD...":GOTO2560
2560 FORJ=1TO500:NEXTJ:PRINT"MINDEN ELSOTETUL ELOTTED... HALOTT VAGY!"
2570 FORJ=1TO2000:NEXTJ
2580 REM" --- VEGE A JATEKNAK ---
2590 PRINT:PRINT:PRINT"UGY TUNIK, NEM SIKERULT A GENGSZTERFONOK";
2600 PRINT"KARMAIBOL ELVE MEGSZABADULNI. A JATEK"
2610 PRINT"VEGET ERT."
2620 PRINT:PRINT"EREDMENYED: "W%"%"
2630 PRINT:PRINT"UJRAPROBALOD (I/N)?"
2640 GETA$:IFA$="I"THENRUN
2650 IFA$<>"N"THEN2640
2660 END
2670 REM" --- TELJESITETTE ---
2680 FORJ=1TO5:PRINT:NEXTJ:PRINT"GRATULALOK, TELJESITETTED A KULDETEST."
2690 PRINT"SIKERULT MEGSZOKNOD A GENGSZTERFONOK"
2700 PRINT"FOGSAGABOL! MOST MAR KOMOLYABB KALAND-"
2710 PRINT"JATEKOKKAL IS PROBALKOZHATSZ (KERESO"
2720 PRINT"TIHOR MIKLOS KALANDJATEKAIT!).":PRINT:PRINT:GOTO2620
2730 REM" --- EGTAJAK ---
2740 DATA "ESZAKRA", "DELRE", "KELETRE", "NYUGATRA"
2750 REM" --- TERKEP ---
2760 DATA 0,0,0,0,1,0,3,0,0,2,0,7,5,0,2,4,0,0,0,0,0,0,4,0,4,0,8,0,0,7,9
2770 DATA 0,8,0,1,0,0,0,0,9,0,0,0,0
2780 REM" --- UTASITASOK ---
2790 DATA "E","D","K","NY","F","A","N","L","LISTA","FOG","RAK","NYIT","HUZ","NEZ","VIZSGAL","TOR"
2800 DATA "BEKAPCSOL","LOK","MOZDIT","ONT","GYUJT","TAMASZT","HASZNAL"
2810 DATA "OLVAS","BERAK","VAR"
2820 REM" --- TARGYAK ---
2830 DATA "DROT", "LEVEL", "GYUFA", "VASKULCS", "BENZINESKANNA", "SZEKRENYKULCS"
2840 DATA "KODKARTYA", "GERENDA"
2850 REM" --- EGYEB FONEVEK ---
2860 DATA "FOTEL", "SZEKRENY", "VASAJTO", "AJTO", "ASZTAL", "FIOK", "SZAMITOGEP"
2870 DATA "POLC", "UVEGSZEKRENY", "SZOBA", "BENZIN"
2880 DATA 1,1,1,1,1,1,10,10,6,0,0
3999 REM --- SPLASH SCREEN LOGIC ---
4000 PRINT CHR$(147):PRINT:PRINT
4005 PRINT TAB(12);CHR$(18);"GENGSZTER";CHR$(146)
4010 PRINT TAB(12);"========="
4015 PRINT TAB(12);"  ,--------"
4020 PRINT TAB(12);"  I -------/"
4025 PRINT TAB(12);"  I I"
4030 PRINT TAB(12);" -I I"
4035 PRINT TAB(11);"  ---    1986"
4040 PRINT:PRINT"IRANYITAS:"
4045 PRINT" MOZGAS: E, D, K, NY"
4050 PRINT" PARANCS: IGE + FONEV (PL. NYIT AJTO)"
4055 PRINT" EKEZET NELKUL IRJ MINDENT!"
4060 PRINT:PRINT"IGEK:"
4065 PRINT" VIZSGAL, NYIT, FOG, RAK, HUZ, OLVAS"
4070 PRINT" TOR, MOZDIT, GYUJT, ONT, HASZNAL"
4075 PRINT:PRINT TAB(5);CHR$(18);"NYOMJ EGY GOMBOT A KEZDESHEZ!";CHR$(146)
4080 POKE 198,0
4085 GET K$:IF K$="" THEN 4085
4090 PRINT CHR$(147)
4095 RETURN
10 REM TERROR TOWN ROB VONK 2022
20 REM BASED ON A RPG WRITTEN BY ERRIN FAMIGLIA AND RONALD PEHR 2006
30 REM HTTPS://BOARDGAMEGEEK.COM/BOARDGAME/320921/TERROR-TOWN-RPG
40 POKE 53280,0:POKE 53281,0:POKE 646,1
50 PRINT"":PRINT"YOU TAKE THE ROLE OF A PERSON TRAPPED"
60 PRINT"IN THE EVIL TERROR TOWN, A GHOST TOWN"
70 PRINT"WHOSE LEGEND CLAIMS A CORRUPT MAYOR"
80 PRINT"SACRIFICED THE SOULS OF HIS ENTIRE TOWN"
90 PRINT"IN ORDER TO GAIN POWER AND ETERNAL LIFE"
100 PRINT"IN A DARK DIMENSION OF PURE EVIL."
110 PRINT:PRINT"THE TOWN EXISTS IN THREE DIFFERENT"
120 PRINT"WORLDS AT ONCE: OUR WORLD, GREY WORLD,"
130 PRINT"AND DARK WORLD. IN OUR WORLD, YOU WILL"
140 PRINT"FIND WEAPONS AND OTHER USEFUL ITEMS."
150 PRINT"IN DARK WORLD, YOU WILL FIND TWISTED"
160 PRINT"MONSTERS OUT TO KILL YOU. IN GREY"
170 PRINT"WORLD, YOU WILL FIND BOTH."
180 PRINT:PRINT"CAN YOU SURVIVE THE SURVIVAL HORROR"
190 PRINT"CHALLENGE OF TERROR TOWN? WILL YOU"
200 PRINT"ESCAPE THE DOOMED TOWN, DEFEAT ITS"
210 PRINT"DIABOLICAL MAYOR, OR JUST END UP"
220 PRINT"FALLING VICTIM TO ITS MANY MONSTERS?"
230 PRINT"THE ANSWER AWAITS YOU...IN TERROR TOWN."
240 PRINT:PRINT"HIT A KEY."
250 GET I$:IF I$="" THEN 250
260 Z=RND(-TI):DEF FNR(X)=INT(RND(1)*X+1)
270 WO=1:WE$=" KNIFE":SH=1:DA=3:KE=0
280 ES=FNR(6)+6:LI=FNR(6)+FNR(6)+12:SL=LI
290 PRINT"":PRINT"YOU START THIS ADVENTURE IN OUR WORLD."
300 PRINT:PRINT"***************************************"
310 PRINT:PRINT"ESCAPE POINTS";ES
320 PRINT"LIFE POINTS";LI
330 IF KE>0 THEN PRINT"CITY KEYS";KE
340 PRINT"YOU ARE ARMED WITH A";WE$;"."
350 PRINT:PRINT"YOU ARE IN ";
360 IF WO=1 THEN PRINT"OUR WORLD.":GOTO 390
370 IF WO=2 THEN PRINT"GREY WORLD.":GOTO 730
380 PRINT"DARK WORLD.":GOTO 780
390 PRINT:PRINT"HIT A KEY TO SEARCH THE TOWN FOR ITEMS."
400 GET I$:IF I$<>"" THEN 400
410 GET I$:IF I$="" THEN 410
420 PRINT:PRINT"YOU FIND A";
430 RO=FNR(6)+FNR(6)
440 IF RO=2 THEN PRINT" CITY KEY!":KE=KE+1
450 IF RO=3 THEN PRINT" CROSSBOW!":WE$=" CROSSBOW":SH=3:DA=FNR(6)+6
460 IF RO=4 THEN PRINT" MAGNUM!":WE$=" MAGNUM":SH=6:DA=FNR(6)+2
470 IF RO=5 THEN PRINT" SHOTGUN!":WE$=" SHOTGUN":SH=4:DA=FNR(6)+4
480 IF RO=6 THEN PRINT" REVOLVER!":WE$=" REVOLVER":SH=6:DA=FNR(6)+1
490 IF RO=7 THEN PRINT" MED KIT!":LI=LI+FNR(6)+FNR(6):IF LI>SL THEN LI=SL
500 IF RO=8 THEN PRINT" PISTOL!":WE$=" PISTOL":SH=8:DA=FNR(6)
510 IF RO=9 THEN PRINT" RIFLE!":WE$=" RIFLE":SH=4:DA=FNR(6)+3
520 IF RO=10 THEN PRINT"N UZI!":WE$="N UZI":SH=12:DA=FNR(6)+1
530 IF RO=11 THEN PRINT" GRENADE!":WE$=" GRENADE":SH=1:DA=FNR(6)+FNR(6)+6
540 IF RO=12 AND WO=2 THEN 430
550 IF RO<>12 THEN 600
560 PRINT" POWER PORTAL!":IF FNR(6)>KE THEN 580
570 PRINT:PRINT"YOU USE THE PORTAL TO ESCAPE THE CITY!":END
580 PRINT:PRINT"HOWEVER, YOU DON'T HAVE ENOUGH KEYS TO"
590 PRINT"ACTIVATE IT.":GOTO 610
600 IF RO<>2 AND RO<>7 THEN PRINT"IT CAN DEAL";DA;"DAMAGE."
610 PRINT:PRINT"HIT A KEY TO CONTINUE YOUR QUEST."
620 GET I$:IF I$<>"" THEN 620
630 GET I$:IF I$="" THEN 630
640 RO=FNR(6)+FNR(6):IF RO<>2 AND RO<>7 AND RO<>12 THEN 300
650 RO=FNR(6):IF WO<>1 THEN 680
660 IF RO<5 THEN WO=2:GOTO 300
670 WO=3:GOTO 300
680 IF WO<>2 THEN 710
690 IF RO<4 THEN WO=1:GOTO 300
700 WO=3:GOTO 300
710 IF RO<5 THEN WO=2:GOTO 300
720 WO=1:GOTO 300
730 PRINT:PRINT"WHAT WILL YOU FIND HERE?"
740 PRINT"HIT A KEY TO FIND OUT."
750 GET I$:IF I$<>"" THEN 750
760 GET I$:IF I$="" THEN 760
770 RO=FNR(6):IF RO<4 THEN 420
780 MO=FNR(6)+FNR(6):PRINT:PRINT"LOOK OUT!":PRINT
790 IF MO=2 THEN PRINT"A GIANT TENTACLE";:MO$="THE GIANT TENTACLE"
800 IF MO=2 THEN EC=12:EA=0:EL=36:ED=FNR(6)+FNR(6)+3
810 IF MO=3 THEN PRINT"A LEECH CLOUD";:MO$="THE LEECH CLOUD"
820 IF MO=3 THEN EC=10:EA=5:EL=9:ED=FNR(6)+FNR(6)+1
830 IF MO=4 THEN PRINT"A VAMPLEWEED";:MO$="THE VAMPLEWEED"
840 IF MO=4 THEN EC=9:EA=3:EL=15:ED=FNR(6)+5
850 IF MO=5 THEN PRINT"A CHOMPBOX";:MO$="THE CHOMPBOX"
860 IF MO=5 THEN EC=6:EA=4:EL=13:ED=FNR(6)+4
870 IF MO=6 THEN PRINT"A BEEKEEPER";:MO$="THE BEEKEEPER"
880 IF MO=6 THEN EC=8:EA=2:EL=16:ED=FNR(6)+1
890 IF MO=7 THEN PRINT"A DROOLER";:MO$="THE DROOLER"
900 IF MO=7 THEN EC=7:EA=1:EL=12:ED=FNR(6)
910 IF MO=8 THEN PRINT"TALL SALLY";:MO$="TALL SALLY"
920 IF MO=8 THEN EC=9:EA=2:EL=14:ED=FNR(6)+2
930 IF MO=9 THEN PRINT"A KILLBOARD";:MO$="THE KILLBOARD"
940 IF MO=9 THEN EC=6:EA=1:EL=27:ED=FNR(6)+3
950 IF MO=10 THEN PRINT"A THREETTIN";:MO$="THE THREETTIN"
960 IF MO=10 THEN EC=8:EA=3:EL=18:ED=FNR(6)+6
970 IF MO=11 THEN PRINT"A WORMFACE";:MO$="THE WORMFACE"
980 IF MO=11 THEN EC=7:EA=4:EL=21:ED=FNR(6)+FNR(6)
990 IF MO=12 THEN PRINT"THE DARK MAYOR";:MO$="THE DARK MAYOR"
1000 IF MO=12 THEN EC=11:EA=6:EL=24:ED=FNR(6)+FNR(6)+2
1010 PRINT" IS HERE!":PRINT:PRINT MO$;"'S STATS:"
1020 PRINT"CHASE";EC;"ARMOUR";EA;"LIFE";EL;"DAMAGE";ED
1030 PRINT:PRINT"DO YOU WISH TO (1) FLEE OR (2) FIGHT";
1040 INPUT IN:IF IN<>1 AND IN<>2 THEN PRINT:PRINT"(1) OR (2) PLEASE.":GOTO 1040
1050 IF IN=2 THEN 1180
1060 PRINT:PRINT"YOU ATTEMPT TO FLEE ";MO$;"..."
1070 FOR I=1 TO 1000:NEXT I:R1=FNR(6)+FNR(6):IF R1>=ES THEN 1090
1080 PRINT"YOU SUCCEED!":FOR I=1 TO 1000:NEXT I:GOTO 1100
1090 PRINT"YOU FAIL THOUGH.":FOR I=1 TO 1000:NEXT I
1100 PRINT:PRINT MO$;" GIVES CHASE...":FOR I=1 TO 1000:NEXT I
1110 R2=FNR(6)+FNR(6):IF R2<EC THEN 1130
1120 PRINT MO$;" FAILS TO CATCH YOU!":GOTO 1140
1130 PRINT MO$;" CATCHES YOU!"
1140 IF R1>=ES AND R2<EC THEN 1170
1150 IF (R1>=ES AND R2>=EC) OR (R1<ES AND R2<EC) THEN 1030
1160 PRINT"YOU ESCAPE ";MO$;"'S CLUTCHES!":GOTO 610
1170 PRINT"YOU MUST FIGHT ";MO$;"!"
1180 PRINT:PRINT"YOU HAVE";SH;"SHOT";:IF SH>1 THEN PRINT"S ";:GOTO 1200
1190 PRINT" ";
1200 PRINT"LEFT OF YOUR";
1210 IF RIGHT$(WE$,3)="UZI" THEN PRINT" UZI.":GOTO 1230
1220 PRINT WE$;"."
1230 PRINT:PRINT MO$;" HAS";EL;"LIFE POINT";:IF EL<>1 THEN PRINT"S";:GOTO 1240
1240 PRINT".":PRINT"YOU HAVE";LI
1250 PRINT:PRINT"HOW MANY SHOTS DO YOU WISH TO TAKE";
1260 INPUT IN
1270 IF IN<1 THEN PRINT:PRINT"YOU MUST TAKE AT LEAST ONE SHOT!":GOTO 1260
1280 IF IN>SH THEN PRINT:PRINT"YOU DON'T HAVE THAT MANY SHOTS!":GOTO 1260
1290 FOR I=1 TO IN:IF RIGHT$(WE$,3)<>"IFE" THEN SH=SH-1
1300 PRINT:PRINT"YOU ATTACK ";MO$;"...":FOR J=1 TO 1000:NEXT J
1310 RO=FNR(6):IF RO<EA THEN 1360
1320 PRINT"YOU HIT ";MO$;" FOR";DA;"DAMAGE!":EL=EL-DA
1330 PRINT MO$;" NOW HAS";EL;"LIFE POINT";:IF EL<>1 THEN PRINT"S";:GOTO 1340
1340 PRINT".":IF EL<=0 THEN 1470
1350 GOTO 1380
1360 IF RND(1)>.5 THEN PRINT"YOU MISS!":GOTO 1380
1370 PRINT"YOUR SHOT IS INEFFECTIVE!"
1380 FOR J=1 TO 1000:NEXT J:PRINT:PRINT MO$;" ATTACKS!":FOR J=1 TO 1000:NEXT J
1390 LI=LI-ED:PRINT"YOU NOW HAVE";LI;"LIFE POINT";
1400 IF LL<>1 THEN PRINT"S";:GOTO 1420
1410 PRINT" ";
1420 PRINT".":FOR J=1 TO 1000:NEXT J:IF LI<=0 THEN 1530
1430 IF SH>0 THEN 1460
1440 PRINT:PRINT"YOU'RE OUT OF SHOTS, SO YOU MUST FIGHT"
1450 PRINT MO$;" WITH A KNIFE!":WE$=" KNIFE":SH=1:DA=3:FOR J=1 TO 1000:NEXT J
1460 NEXT I:GOTO 1030
1470 IF MO=12 THEN 1490
1480 PRINT:PRINT"YOU HAVE SLAIN ";MO$;"!":GOTO 610
1490 PRINT:PRINT"THE DARK MAYOR LIES DEAD AT YOUR FEET,"
1500 PRINT"HIS POWER OVER THE TOWN SHATTERED."
1510 PRINT"NEVER AGAIN WILL THE TOWNSPEOPLE SUFFER"
1520 PRINT"AT THE HANDS OF SUCH EVIL. WELL DONE!":END
1530 PRINT:PRINT"YOU ARE DEAD. THE DARK MAYOR REIGNS"
1540 PRINT"SUPREME OVER THE TOWN!"
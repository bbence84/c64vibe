10 REM COMMODORE 64 PORT OF WIZARD'S CASTLE
11 PRINT CHR$(147);:POKE 53280,0:POKE 53281,0:POKE 646,1
20 DIM C$(34),I$(34),R$(4),W$(8),E$(8)
30 DIM C(3,4),T(8),O(3),R(3):PRINT"CREATING ARRAYS..."
40 X=RND(-TI) :REM SEED RNG
50 DEF FND(Q)=49152+(Q-1)*64+(X-1)*8+(Y-1):DEF FNE(Q)=Q+100*(Q>99)
60 Y$=CHR$(13)+"** ANSWER YES OR NO "+CHR$(13)
70 DEF FNA(Q)=1+INT(RND(1)*Q):DEF FNB(Q)=Q+8*((Q=9)-(Q=0))
80 RESTORE:FOR Q=1 TO 34:READ C$(Q),I$(Q):NEXT Q
90 FOR Q=49152 TO 49663:POKE Q,101:NEXT:FOR Q=1 TO 8:READ W$(Q),E$(Q):NEXT
100 FOR Q=1 TO 4:READ R$(Q):NEXT Q:DEF FNC(Q)=-Q*(Q<19)-18*(Q>18)
110 PRINT CHR$(147):GOSUB 3270:PRINT TAB(10)"THE WIZARD'S CASTLE":PRINT:GOSUB 3270
120 PRINT"COPYRIGHT (C) 1980 BY JOSEPH R POWER":PRINT
130 PRINT"LAST REVISED - 04/12/80 11:10 PM":PRINT
140 PRINT:PRINT"PLEASE BE PATIENT - ";:X=1:Y=4
150 POKE FND(1),2:PRINT"IN";:FOR Z=1 TO 7:FOR Q1=1 TO 2:Q=104:GOSUB 3200
160 POKE FND(Z+1),103:NEXT Q1:NEXT Z:PRINT"I";
170 FOR Z=1 TO 8:FOR Q=113 TO 124:GOSUB 3200:NEXT Q:FOR Q1=1 TO 3
180 FOR Q=105 TO 112:GOSUB 3200:NEXT Q:Q=125:GOSUB 3200:NEXT Q1:READ O$:PRINT O$;:NEXT Z
190 FOR Q=126 TO 133:Z=FNA(8):GOSUB 3200:NEXT Q:PRINT"G";
200 Q=101:FOR A=1 TO 3:Z=FNA(8):GOSUB 3200:C(A,1)=X:C(A,2)=Y:C(A,3)=Z:C(A,4)=0
210 READ O$:PRINT O$;:NEXT A:PRINT"S";:RC=0:SR=2:DX=14:R$(3)="MAN"
220 Q=112+FNA(12):Z=FNA(8):GOSUB 3200:R(1)=X:R(2)=Y:R(3)=Z
230 Q=109:Z=FNA(8):GOSUB 3200:O(1)=X:O(2)=Y:O(3)=Z:PRINT"TLE"
240 BF=0:OT=8:AV=0:HT=0:T=1:VF=0:LF=0:TC=0:GP=60:RF=0:OF=0:BL=0
250 IQ=8:SX=0:FOR Q=1 TO 8:T(Q)=0:NEXT Q:PRINT CHR$(147);"ALL RIGHT, BOLD ONE"
260 PRINT:PRINT"YOU MAY BE AN ELF, DWARF, MAN, OR HOBBIT":GOSUB 3280
270 FOR Q=1 TO 4:IF LEFT$(R$(Q),1)=O$ THEN RC=Q:SR=SR+2*Q:DX=DX-2*Q
280 NEXT Q:PRINT:OT=OT+4*(RC=1):IF RC>0 THEN R$(3)="HUMAN":GOTO 300
290 PRINT"** THAT WAS INCORRECT. PLEASE TYPE E, D, M, OR H.":GOTO 260
300 PRINT:PRINT"SEX ";:GOSUB 3290:IF O$="M" THEN SX=1:GOTO 320
310 IF O$<>"F" THEN PRINT:PRINT"** CUTE ";R$(RC);", REAL CUTE. TRY M OR F":GOTO 300
320 PRINT CHR$(147):PRINT"OK ";R$(RC);", YOU HAVE THESE STATISTICS:":PRINT
330 PRINT"STRENGTH= ";SR;" INTELLIGENCE= ";IQ;" DEXTERITY= ";DX:PRINT
340 PRINT"AND ";OT;" OTHER POINTS TO ALLOCATE AS YOU WISH.":PRINT
350 Z$="INTELLIGENCE ":GOSUB 3300:IQ=IQ+Q:IF OT=0 THEN 370
360 Z$="STRENGTH ":GOSUB 3300:SR=SR+Q:IF OT THEN Z$="DEXTERITY ":GOSUB 3300:DX=DX+Q
370 PRINT CHR$(147):PRINT"OK, ";R$(RC);", YOU HAVE 60 GOLD PIECES (GP'S)":PRINT
380 Z$="ARMOR":GOSUB 3390:AV=0:WV=0:FL=0:WC=0
390 PRINT"PLATE<30> CHAINMAIL<20> LEATHER<10> NOTHING<0>"
400 GOSUB 3280:IF O$="N" THEN 440
410 AV=-3*(O$="P")-2*(O$="C")-(O$="L"):IF AV>0 THEN 440
420 PRINT:PRINT"** ARE YOU A ";R$(RC);" OR ";C$(FNA(12)+12);
430 PRINT" ? TYPE P,C,L, OR N":PRINT:GOTO 380
440 AH=AV*7:GP=GP-AV*10:PRINT CHR$(147)
450 PRINT:PRINT"OK, BOLD ";R$(RC);", YOU HAVE ";GP;" GP'S LEFT":PRINT
460 Z$="WEAPONS":GOSUB 3390
470 PRINT"SWORD<30> MACE<20> DAGGER<10> NOTHING<0>":GOSUB 3280:IF O$="N" THEN 500
480 WV=-3*(O$="S")-2*(O$="M")-(O$="D"):IF WV>0 THEN 500
490 PRINT:PRINT"** IS YOUR IQ REALLY ";IQ;"? TYPE S, M, D, OR N":PRINT:GOTO 460
500 GP=GP-WV*10:PRINT CHR$(147):IF GP<20 THEN 540
510 PRINT"WANT TO BUY A LAMP FOR 20 GP'S ";:GOSUB 3290
520 IF O$="Y" THEN LF=1:GP=GP-20:GOTO 540
530 IF O$<>"N" THEN PRINT:PRINT Y$:PRINT:GOTO 510
540 PRINT CHR$(147):IF GP<1 THEN Q=0:GOTO 600
550 PRINT:PRINT"OK, ";R$(RC);", YOU HAVE ";GP;" GOLD PIECES LEFT":PRINT
560 INPUT"FLARES COST 1 GP EACH. HOW MANY DO YOU WANT ";O$
570 Q=VAL(O$):PRINT:IF Q>0 OR ASC(O$)=48 THEN 590
580 PRINT"** IF YOU DON'T WANT ANY JUST TYPE 0 (ZERO)":PRINT:GOTO 560
590 IF Q>GP THEN PRINT"** YOU CAN ONLY AFFORD ";GP:PRINT:GOTO 560
600 FL=FL+Q:GP=GP-Q:PRINT CHR$(147):X=1:Y=4:Z=1
610 PRINT"OK ";R$(RC);", YOU ENTER THE CASTLE AND BEGIN.":PRINT:GOTO 1670
620 T=T+1:IF RF+OF>0 THEN 690
630 IF C(1,4)>T(1) THEN T=T+1
640 IF C(2,4)>T(3) THEN GP=GP-FNA(5):IF GP<0 THEN GP=0
650 IF C(3,4)<=T(5) THEN 690
660 A=X:B=Y:C=Z:X=FNA(8):Y=FNA(8):Z=FNA(8)
670 POKE FND(Z),FNE(PEEK(FND(Z)))+100:X=A:Y=B:Z=C
680 IF PEEK(FND(Z))=1 THEN FOR Q=1 TO 3:C(Q,4)=-(C(Q,1)=X)*(C(Q,2)=Y)*(C(Q,3)=Z):NEXT
690 IF FNA(5)>1 THEN 790
700 PRINT:PRINT"YOU ";:Q=FNA(7)+BL:IF Q>7 THEN Q=4
710 ON Q GOSUB 750,730,740,720,760,770,780:GOTO 790
720 PRINT"STEPPED ON A FROG":RETURN
730 PRINT"HEAR ";MID$("A SCREAM FOOTSTEPSA WUMPUS THUNDER",FNA(4)*9-8,9):RETURN
740 PRINT"SNEEZED":RETURN
750 PRINT"SEE A BAT FLY BY":RETURN
760 PRINT"SMELL ";C$(12+FNA(13));" FRYING":RETURN
770 PRINT"FEEL LIKE YOU'RE BEING WATCHED":RETURN
780 PRINT"ARE PLAYING WIZARD'S CASTLE":RETURN
790 IF BL+T(4)=2 THEN PRINT:PRINT C$(29);" CURES YOUR BLINDNESS":BL=0
800 IF BF+T(6)=2 THEN PRINT:PRINT C$(31);" DISSOLVES THE BOOK":BF=0
810 PRINT:PRINT:PRINT:INPUT"YOUR MOVE ";O$:IF LEFT$(O$,2)="DR" THEN 1180
820 O$=LEFT$(O$,1):IF O$="N" THEN 940
830 IF (O$="S") OR (O$="W") OR (O$="E") THEN 950
840 IF O$="U" THEN 970
850 IF O$="D" THEN 990
860 IF O$="M" THEN 1010
870 IF O$="F" THEN ON 1+BL GOTO 1070,1010
880 IF O$="L" THEN ON 1+BL GOTO 1110,1010
890 IF O$="O" THEN 1310
900 IF O$="G" THEN ON 1+BL GOTO 1480,1010
910 IF O$="T" THEN PRINT:ON 1+RF GOTO 1590,1600
920 IF O$="Q" THEN 1640
930 PRINT:PRINT"** STUPID ";R$(RC);" THAT WASN'T A VALID COMMAND":GOTO 620
940 IF PEEK(FND(Z))=2 THEN 2900
950 X=X+(O$="N")-(O$="S"):Y=Y+(O$="W")-(O$="E")
960 X=FNB(X):Y=FNB(Y):GOTO 1670
970 IF PEEK(FND(Z))=3 THEN Z=Z-1:GOTO 1670
980 Z$="UP":GOTO 1000
990 Z$="DOWN":IF PEEK(FND(Z))=4 THEN Z=Z+1:GOTO 1670
1000 PRINT:PRINT"** OH ";R$(RC);", NO STAIRS GOING ";Z$;" IN HERE":GOTO 620
1010 IF BL=1 THEN PRINT:PRINT"** YOU CAN'T SEE ANYTHING, DUMB ";R$(RC):GOTO 620
1020 PRINT:PRINT:A=X:B=Y:FOR X=1 TO 8:FOR Y=1 TO 8:Q=PEEK(FND(Z)):IF Q>99 THEN Q=34
1030 IF X=A AND Y=B THEN PRINT"<";I$(Q);">";:GOTO 1050
1040 PRINT" ";I$(Q);" ";
1050 NEXT Y:PRINT:PRINT:NEXT X:X=A:Y=B:GOTO 1100
1060 PRINT") LEVEL ";Z:GOTO 620
1070 IF FL=0 THEN PRINT:PRINT"** HEY BRIGHT ONE, YOU'RE OUT OF FLARES":GOTO 620
1080 PRINT:PRINT:FL=FL-1:A=X:B=Y:FOR Q1=A-1 TO A+1:X=FNB(Q1):FOR Q2=B-1 TO B+1:Y=FNB(Q2)
1090 Q=FNE(PEEK(FND(Z))):POKE FND(Z),Q:PRINT I$(Q);"  ";:NEXT Q2:PRINT:PRINT:NEXT Q1:X=A:Y=B
1100 GOSUB 3400:GOTO 620
1110 IF LF=0 THEN PRINT:PRINT"** YOU DON'T HAVE A LAMP, ";R$(RC):GOTO 620
1120 PRINT:PRINT"WHERE DO YOU SHINE THE LAMP (N,S,E, OR W) ";:GOSUB 3290
1130 A=X:B=Y:X=FNB(X+(O$="N")-(O$="S")):Y=FNB(Y+(O$="W")-(O$="E"))
1140 IF A-X+B-Y=0 THEN PRINT:PRINT"** TURKEY! THAT'S NOT A DIRECTION":GOTO 620
1150 PRINT:PRINT"THE LAMP SHINES INTO (";X;",";Y;") LEVEL ";Z:PRINT
1160 POKE FND(Z),FNE(PEEK(FND(Z))):PRINT"THERE YOU WILL FIND ";C$(PEEK(FND(Z)))
1170 X=A:Y=B:GOTO 620
1180 IF PEEK(FND(Z))<>5 THEN PRINT:PRINT"** IF YOU WANT A DRINK, FIND A POOL":GOTO 620
1190 Q=FNA(8):PRINT:PRINT"YOU TAKE A DRINK AND ";:IF Q<7 THEN PRINT"FEEL ";
1200 ON Q GOTO 1210,1220,1230,1240,1250,1260,1270,1290
1210 SR=FNC(SR+FNA(3)):PRINT"STRONGER":GOTO 620
1220 SR=SR-FNA(3):PRINT"WEAKER":ON 1-(SR<1) GOTO 620,2840
1230 IQ=FNC(IQ+FNA(3)):PRINT"SMARTER":GOTO 620
1240 IQ=IQ-FNA(3):PRINT"DUMBER":ON 1-(IQ<1) GOTO 620,2840
1250 DX=FNC(DX+FNA(3)):PRINT"NIMBLER":GOTO 620
1260 DX=DX-FNA(3):PRINT"CLUMSIER":ON 1-(DX<1) GOTO 620,2840
1270 Q=FNA(4):IF Q=RC THEN 1270
1280 RC=Q:PRINT"BECOME A ";R$(RC):GOTO 620
1290 SX=1-SX:PRINT"TURN INTO A ";:IF SX=0 THEN PRINT"FE";
1300 PRINT"MALE ";R$(RC):GOTO 620
1310 IF PEEK(FND(Z))=6 THEN PRINT:PRINT"YOU OPEN THE CHEST AND":PRINT:GOTO 1430
1320 IF PEEK(FND(Z))=12 THEN PRINT:PRINT"YOU OPEN THE BOOK AND":PRINT:GOTO 1340
1330 PRINT:PRINT"** THE ONLY THING YOU OPENED WAS YOUR BIG MOUTH":GOTO 620
1340 ON FNA(6) GOTO 1350,1360,1370,1380,1390,1400
1350 PRINT"FLASH! OH NO! YOU ARE NOW A BLIND ";R$(RC):BL=1:GOTO 1420
1360 PRINT"ITS ANOTHER VOLUME OF ZOT'S POETRY! - YEECH!":GOTO 1420
1370 PRINT"ITS AN OLD COPY OF PLAY";R$(FNA(4)):GOTO 1420
1380 PRINT"ITS A MANUAL OF DEXTERITY!":DX=18:GOTO 1420
1390 PRINT"ITS A MANUAL OF STRENGTH !":SR=18:GOTO 1420
1400 PRINT"THE BOOK STICKS TO YOUR HANDS -":PRINT
1410 PRINT"NOW YOU CAN'T DRAW YOUR WEAPON!":BF=1
1420 POKE FND(Z),1:GOTO 620
1430 ON FNA(4) GOTO 1440,1450,1460,1450
1440 PRINT"KABOOM! IT EXPLODES":Q=FNA(6):GOSUB 2800:ON 1-(SR<1) GOTO 1420,2840
1450 Q=FNA(1000):PRINT"FIND ";Q;" GOLD PIECES":GP=GP+Q:GOTO 1420
1460 PRINT"GAS! YOU STAGGER FROM THE ROOM"
1470 POKE FND(Z),1:T=T+20:O$=MID$("NSEW",FNA(4),1):GOTO 950
1480 IF PEEK(FND(Z))<>11 THEN PRINT:PRINT"** NO ORB - NO GAZE":GOTO 620
1490 PRINT:PRINT"YOU SEE ";:ON FNA(6) GOTO 1500,1510,1530,1540,1560,1580
1500 PRINT"YOURSELF IN A BLOODY HEAP":SR=SR-FNA(2):ON 1-(SR<1) GOTO 620,2840
1510 PRINT"YOURSELF DRINKING FROM A POOL AND BECOMING ";C$(12+FNA(13))
1520 GOTO 620
1530 PRINT C$(12+FNA(13));" GAZING BACK AT YOU":GOTO 620
1540 A=X:B=Y:C=Z:X=FNA(8):Y=FNA(8):Z=FNA(8):Q=FNE(PEEK(FND(Z))):POKE FND(Z),Q
1550 PRINT C$(Q);" AT (";X;",";Y;") LEVEL ";Z:X=A:Y=B:Z=C:GOTO 620
1560 A=FNA(8):B=FNA(8):C=FNA(8):IF FNA(8)<4 THEN A=O(1):B=O(2):C=O(3)
1570 PRINT"THE ORB OF ZOT AT (";A;",";B;") LEVEL ";C:GOTO 620
1580 PRINT"A SOAP OPERA RERUN":GOTO 620
1590 IF RF=0 THEN PRINT:PRINT"** YOU CAN'T TELEPORT WITHOUT THE RUNESTAFF!":GOTO 620
1600 Z$="X-COORD (1=FAR NORTH 8=FAR SOUTH) ":GOSUB 3350:X=Q
1610 Z$="Y-COORD (1=FAR WEST 8=FAR EAST ) ":GOSUB 3350:Y=Q
1620 Z$="LEVEL (1=TOP 8=BOTTOM ) ":GOSUB 3350:Z=Q
1630 O$="T":GOTO 1670
1640 PRINT:PRINT"DO YOU REALLY WANT TO QUIT ";:GOSUB 3290:PRINT
1650 IF O$<>"Y" THEN PRINT"** THEN DON'T SAY YOU DO":GOTO 620
1660 PRINT:PRINT:GOTO 2940
1670 PRINT:IF BL=0 THEN GOSUB 3400:PRINT
1680 PRINT"SR= ";SR;" IQ= ";IQ;" DX= ";DX;" FLARES= ";FL;" GP'S= ";GP
1690 PRINT:PRINT W$(WV+1);" / ";W$(AV+5);:IF LF=1 THEN PRINT" / A LAMP";
1700 PRINT:PRINT:WC=0:Q=FNE(PEEK(FND(Z))):POKE FND(Z),Q:Z$="YOU HAVE "
1710 PRINT"HERE YOU FIND ";C$(Q):IF (Q<7) OR (Q=11) OR (Q=12) THEN 620
1720 IF Q=7 THEN GP=GP+FNA(10):PRINT:PRINT Z$;GP:GOTO 1420
1730 IF Q=8 THEN FL=FL+FNA(5):PRINT:PRINT Z$;FL:GOTO 1420
1740 IF Q>9 THEN 1770
1750 PRINT:IF (O(1)=X) AND (O(2)=Y) AND (O(3)=Z) THEN ON 1-(O$="T") GOTO 950,3050
1760 X=FNA(8):Y=FNA(8):Z=FNA(8):GOTO 1670
1770 IF Q=10 THEN Z=FNB(Z+1):GOTO 1670
1780 IF Q>25 AND Q<34 THEN PRINT:PRINT"ITS NOW YOURS":T(Q-25)=1:TC=TC+1:GOTO 1420
1790 A=PEEK(FND(Z))-12:WC=0:IF (A<13) OR (VF=1) THEN 2300
1800 PRINT:PRINT"YOU MAY TRADE WITH, ATTACK, OR IGNORE THE VENDOR"
1810 GOSUB 3280:IF O$="I" THEN 620
1820 IF O$="A" THEN VF=1:PRINT:PRINT"YOU'LL BE SORRY YOU DID THAT":GOTO 2300
1830 IF O$<>"T" THEN PRINT:PRINT"** NICE SHOT, ";R$(RC):GOTO 1800
1840 PRINT:FOR Q=1 TO 8:A=FNA(Q*1500):IF T(Q)=0 THEN 1880
1850 PRINT:PRINT"DO YOU WANT TO SELL ";C$(Q+25);" FOR ";A;" GP'S ";
1860 GOSUB 3290:IF O$="Y" THEN TC=TC-1:T(Q)=0:GP=GP+A:GOTO 1880
1870 IF O$<>"N" THEN PRINT Y$:GOTO 1850
1880 NEXT Q
1890 IF GP<1E3 THEN PRINT:PRINT"YOU'RE TOO POOR TO TRADE, ";R$(RC):GOTO 620
1900 IF GP<1250 THEN 2130
1910 PRINT:PRINT"OK, ";R$(RC);", YOU HAVE ";GP;" GOLD PIECES AND ";W$(AV+5)
1920 PRINT:Z$="ARMOR":GOSUB 3390:PRINT"NOTHING<0> LEATHER<1250> ";
1930 IF GP>1499 THEN PRINT"CHAINMAIL<1500> ";:IF GP>1999 THEN PRINT"PLATE<2000>";
1940 PRINT:GOSUB 3280:PRINT:IF O$="N" THEN 2010
1950 IF O$="L" THEN GP=GP-1250:AV=1:AH=7:GOTO 2010
1960 IF O$="C" AND GP<1500 THEN PRINT"** YOU HAVEN'T GOT THAT MUCH CASH":GOTO 1920
1970 IF O$="C" THEN GP=GP-1500:AV=2:AH=14:GOTO 2010
1980 IF O$="P" AND GP<2000 THEN PRINT"** YOU CAN'T AFFORD PLATE":GOTO 1920
1990 IF O$="P" THEN GP=GP-2000:AV=3:AH=21:GOTO 2010
2000 PRINT:PRINT"** DON'T BE SILLY. CHOOSE A SELECTION":GOTO 1940
2010 IF GP<1250 THEN 2130
2020 PRINT:PRINT"YOU HAVE ";GP;" GP'S LEFT WITH ";W$(WV+1);" IN HAND":PRINT
2030 PRINT:Z$="WEAPON":GOSUB 3390:PRINT"NOTHING<0> DAGGER<1250> ";
2040 IF GP>1499 THEN PRINT"MACE<1500> ";:IF GP>1999 THEN PRINT"SWORD<2000>";
2050 PRINT:GOSUB 3280:PRINT:IF O$="N" THEN 2130
2060 IF O$="D" THEN GP=GP-1250:WV=1:GOTO 2130
2070 IF O$="M" AND GP<1500 THEN PRINT"** SORRY SIR, I DON'T GIVE CREDIT":GOTO 2030
2080 IF O$="M" THEN GP=GP-1500:WV=2:GOTO 2130
2090 IF O$="S" AND GP<2000 THEN PRINT"** DUNGEON EXPRESS CARD - ";
2100 IF O$="S" AND GP<2000 THEN PRINT"YOU LEFT HOME WITHOUT IT!":GOTO 2030
2110 IF O$="S" THEN GP=GP-2000:WV=3:GOTO 2130
2120 PRINT"** TRY CHOOSING A SELECTION":GOTO 2050
2130 IF GP<1000 THEN 620
2140 Z$="STRENGTH":GOSUB 3370:IF O$<>"Y" THEN 2160
2150 GP=GP-1E3:SR=FNC(SR+FNA(6)):Q=SR:GOSUB 3380:GOTO 2130
2160 IF O$<>"N" THEN PRINT Y$:GOTO 2140
2170 IF GP<1000 THEN 620
2180 Z$="INTELLIGENCE":GOSUB 3370:IF O$<>"Y" THEN 2200
2190 GP=GP-1E3:IQ=FNC(IQ+FNA(6)):Q=IQ:GOSUB 3380:GOTO 2170
2200 IF O$<>"N" THEN PRINT Y$:GOTO 2180
2210 IF GP<1000 THEN 620
2220 Z$="DEXTERITY":GOSUB 3370:IF O$<>"Y" THEN 2240
2230 GP=GP-1E3:DX=FNC(DX+FNA(6)):Q=DX:GOSUB 3380:GOTO 2210
2240 IF O$<>"N" THEN PRINT Y$:GOTO 2220
2250 IF (GP<1000) OR (LF=1) THEN 620
2260 PRINT:PRINT"WANT A LAMP FOR 1000 GP'S ";:GOSUB 3290:IF O$<>"Y" THEN 2280
2270 GP=GP-1000:LF=1:PRINT:PRINT"ITS GUARANTEED TO OUTLIVE YOU!":GOTO 620
2280 IF O$<>"N" THEN PRINT Y$:GOTO 2260
2290 GOTO 620
2300 Q1=1+INT(A/2):Q2=A+2:Q3=1
2310 IF (C(1,4)>T(1)) OR (BL=1) OR (DX<FNA(9)+FNA(9)) THEN 2690
2320 PRINT:PRINT:PRINT"YOU'RE FACING ";C$(A+12):PRINT:PRINT"YOU MAY ATTACK OR RETREAT";
2330 IF Q3=1 THEN PRINT", OR BRIBE";:IF IQ>14 THEN PRINT", OR CAST A SPELL";
2340 PRINT:PRINT:PRINT"YOUR STRENGTH IS ";SR;" AND DEXTERITY IS ";DX:PRINT:GOSUB 3280
2350 IF O$<>"A" THEN 2480
2360 IF WV=0 THEN PRINT:PRINT"** POUNDING ON ";C$(12+A);" WON'T HURT IT":GOTO 2690
2370 IF BF=1 THEN PRINT:PRINT"** YOU CAN'T BEAT IT TO DEATH WITH A BOOK":GOTO 2690
2380 IF DX<FNA(20)+(BL*3) THEN PRINT:PRINT"  DRAT! MISSED":GOTO 2690
2390 PRINT:PRINT"  YOU HIT THE LOUSY ";RIGHT$(C$(A+12),LEN(C$(A+12))-2):Q2=Q2-WV
2400 IF (A=9 OR A=12) AND FNA(8)=1 THEN PRINT:PRINT"OH NO! YOUR ";W$(WV+1);" BROKE":WV=0
2410 IF Q2>0 THEN 2690
2420 PRINT:MC=MC-1:PRINT C$(A+12);" LIES DEAD AT YOUR FEET":IF H>T-60 THEN 2440
2430 PRINT:PRINT"YOU SPEND AN HOUR EATING ";C$(A+12);E$(FNA(8)):H=T
2440 IF X<>R(1) OR Y<>R(2) OR Z<>R(3) THEN ON 1-(A=13) GOTO 2460,3220
2450 PRINT:PRINT"GREAT ZOT! YOU'VE FOUND THE RUNESTAFF":R(1)=0:RF=1
2460 Q=FNA(1000):PRINT:PRINT"YOU NOW GET HIS HOARD OF ";Q;" GP'S"
2470 GP=GP+Q:GOTO 1420
2480 IF O$="R" THEN 2690
2490 IF (O$<>"C") THEN 2610
2500 IF IQ<15 OR Q3>1 THEN PRINT:PRINT"** YOU CAN'T CAST A SPELL NOW!":GOTO 2320
2510 PRINT:PRINT"WHICH SPELL (WEB, FIREBALL, OR DEATHSPELL) ";:GOSUB 3290:PRINT
2520 IF O$<>"W" THEN 2540
2530 SR=SR-1:WC=FNA(8)+1:ON 1-(SR<1) GOTO 2690,2840
2540 IF O$<>"F" THEN 2580
2550 Q=FNA(7)+FNA(7):SR=SR-1:IQ=IQ-1:IF (IQ<1) OR (SR<1) THEN 2840
2560 PRINT"  IT DOES ";Q;" POINTS OF DAMAGE.":PRINT
2570 Q2=Q2-Q:GOTO 2410
2580 IF O$<>"D" THEN PRINT:PRINT"** TRY ONE OF THE OPTIONS GIVEN":GOTO 2320
2590 PRINT"DEATH - - - ";:IF IQ<15+FNA(4) THEN PRINT"YOURS":IQ=0:GOTO 2840
2600 PRINT"HIS":PRINT:Q2=0:GOTO 2420
2610 IF O$<>"B" OR Q3>1 THEN PRINT:PRINT"** CHOOSE ONE OF THE LISTED OPTIONS":GOTO 2320
2620 IF TC=0 THEN PRINT:PRINT"'ALL I WANT IS YOUR LIFE!'":GOTO 2690
2630 Q=FNA(8):IF T(Q)=0 THEN 2630
2640 PRINT:PRINT"I WANT ";C$(Q+25);", WILL YOU GIVE IT TO ME ";
2650 GOSUB 3290:IF O$="N" THEN 2690
2660 IF O$<>"Y" THEN PRINT Y$:GOTO 2640
2670 T(Q)=0:TC=TC-1:PRINT:PRINT"OK, JUST DON'T TELL ANYONE"
2680 VF=VF+(PEEK(FND(Z))=25):GOTO 620
2690 Q3=2:IF WC>0 THEN WC=WC-1:IF WC=0 THEN PRINT:PRINT"THE WEB JUST BROKE!"
2700 Z$=RIGHT$(C$(12+A),LEN(C$(12+A))-2):IF WC<=0 THEN 2720
2710 PRINT:PRINT"THE ";Z$;" IS STUCK AND CAN'T ATTACK":GOTO 2750
2720 PRINT:PRINT"THE ";Z$;" ATTACKS":IF DX<FNA(7)+FNA(7)+FNA(7)+BL*3 THEN 2740
2730 PRINT:PRINT"  HAH! HE MISSED YOU":GOTO 2750
2740 PRINT:PRINT"  OUCH! HE HIT YOU":Q=Q1:GOSUB 2800:IF SR<1 THEN 2840
2750 IF O$<>"R" THEN 2320
2760 PRINT:PRINT"YOU HAVE ESCAPED":PRINT
2770 PRINT"DO YOU GO NORTH, SOUTH, EAST, OR WEST ";:GOSUB 3290
2780 IF (O$="N") OR (O$="S") OR (O$="E") OR (O$="W") THEN 950
2790 PRINT:PRINT"** DON'T PRESS YOUR LUCK ";R$(RC):PRINT:GOTO 2770
2800 IF AV=0 THEN 2830
2810 Q=Q-AV:AH=AH-AV:IF Q<0 THEN AH=AH-Q:Q=0
2820 IF AH<0 THEN AH=0:AV=0:PRINT:PRINT"YOUR ARMOR IS DESTROYED - GOOD LUCK"
2830 SR=SR-Q:RETURN
2840 FOR Q=1 TO 1500:NEXT Q:PRINT CHR$(147):GOSUB 3270
2850 PRINT"A NOBLE EFFORT, OH FORMERLY LIVING ";R$(RC):PRINT
2860 PRINT"YOU DIED FROM A LACK OF ";:IF SR<1 THEN PRINT"STRENGTH"
2870 IF IQ<1 THEN PRINT"INTELLIGENCE"
2880 IF DX<1 THEN PRINT"DEXTERITY"
2890 PRINT:PRINT:Q3=1:PRINT"WHEN YOU DIED YOU HAD:":PRINT:GOTO 2970
2900 Q3=0:PRINT:PRINT"YOU LEFT THE CASTLE WITH";:IF OF=0 THEN PRINT"OUT";
2910 PRINT" THE ORB OF ZOT":PRINT:IF OF=0 THEN 2940
2920 PRINT:PRINT"A GLORIOUS VICTORY!":PRINT
2930 PRINT"YOU ALSO GOT OUT WITH THE FOLLOWING:":PRINT:GOTO 2960
2940 PRINT:PRINT"A LESS THAN AWE-INSPIRING DEFEAT."
2950 PRINT:PRINT"WHEN YOU LEFT THE CASTLE YOU HAD:":PRINT
2960 IF Q3=0 THEN PRINT"YOUR MISERABLE LIFE"
2970 FOR Q=1 TO 8:IF T(Q)=1 THEN PRINT C$(Q+25)
2980 NEXT Q:PRINT W$(WV+1):PRINT W$(AV+5):IF LF=1 THEN PRINT"A LAMP"
2990 PRINT FL;" FLARES":PRINT GP;" GP'S":IF RF=1 THEN PRINT"THE RUNESTAFF"
3000 PRINT:PRINT"AND IT TOOK YOU ";T;" TURNS!":PRINT
3010 PRINT:PRINT:PRINT"PLAY AGAIN ";:GOSUB 3290:PRINT
3020 IF O$="Y" THEN PRINT"SOME ";R$(RC);"S NEVER LEARN":PRINT:PRINT:GOTO 40
3030 IF O$<>"N" THEN PRINT Y$:GOTO 3010
3040 PRINT"MAYBE DUMB ";R$(RC);" NOT SO DUMB AFTER ALL":PRINT:END
3050 PRINT:PRINT"GREAT UNMITIGATED ZOT!":PRINT
3060 PRINT"YOU JUST FOUND THE ORB OF ZOT!":PRINT
3070 PRINT"THE RUNESTAFF IS GONE":RF=0:OF=1:O(1)=0:GOTO 1420
3080 DATAAN EMPTY ROOM,.,THE ENTRANCE,E,STAIRS GOING UP,U
3090 DATASTAIRS GOING DOWN,D,A POOL,P,A CHEST,C,GOLD PIECES,G
3100 DATAFLARES,F,A WARP,W,A SINKHOLE,S,A CRYSTAL ORB,O
3110 DATAA BOOK,B,A KOBOLD,M,AN ORC,M,A WOLF,M,A GOBLIN,M,AN OGRE,M
3120 DATAA TROLL,M,A BEAR,M,A MINOTAUR,M,A GARGOYLE,M,A CHIMERA,M
3130 DATAA BALROG,M,A DRAGON,M,A VENDOR,V,THE RUBY RED,T
3140 DATATHE NORN STONE,T,THE PALE PEARL,T,THE OPAL EYE,T
3150 DATATHE GREEN GEM,T,THE BLUE FLAME,T,THE PALANTIR,T,THE SILMARIL
3160 DATAT,X,?,NO WEAPON,WICH
3170 DATADAGGER," STEW",MACE," SOUP",SWORD," BURGER",NO ARMOR," ROAST"
3180 DATALEATHER," MUNCHY",CHAINMAIL," TACO",PLATE," PIE"
3190 DATAHOBBIT,ELF,MAN,DWARF,T,I,A,L,I,Z,I,N," ",C,A
3200 X=FNA(8):Y=FNA(8):IF PEEK(FND(Z))<>101 THEN 3200
3210 POKE FND(Z),Q:RETURN
3220 PRINT:PRINT"YOU GET ALL HIS WARES:":PRINT:PRINT"PLATE ARMOR":AV=3:AH=21
3230 PRINT"A SWORD":WV=3:PRINT"A STRENGTH POTION":SR=FNC(SR+FNA(6))
3240 PRINT"AN INTELLIGENCE POTION":IQ=FNC(IQ+FNA(6))
3250 PRINT"A DEXTERITY POTION":DX=FNC(DX+FNA(6)):IF LF=0 THEN PRINT"A LAMP":LF=1
3260 GOTO 2460
3270 FOR Q=1 TO 39:PRINT"*";:NEXT Q:PRINT:PRINT:RETURN
3280 PRINT:PRINT"YOUR CHOICE ";
3290 INPUT O$:O$=LEFT$(O$,1):RETURN
3300 PRINT"HOW MANY POINTS DO YOU ADD TO ";Z$;:INPUT O$:PRINT
3310 Q=VAL(O$):IF (Q=0) AND (ASC(O$)<>48) THEN Q=-1
3320 IF (Q<0) OR (Q>OT) OR (Q<>INT(Q)) THEN PRINT"** ";:GOTO 3300
3330 OT=OT-Q:RETURN
3340 INPUT O$:Q=INT(VAL(O$)):RETURN
3350 PRINT:PRINT Z$;:INPUT O$:Q=INT(VAL(O$)):IF (Q>0) AND (Q<9) THEN RETURN
3360 PRINT:PRINT"** TRY A NUMBER FROM 1 TO 8":GOTO 3350
3370 PRINT:PRINT"WANT TO BUY A POTION OF ";Z$;" FOR 1000 GP'S ";:GOTO 3290
3380 PRINT:PRINT"YOUR ";Z$;" IS NOW ";Q:RETURN
3390 PRINT"HERE IS A LIST OF ";Z$;" YOU CAN BUY (WITH COST IN <>)":PRINT:RETURN
3400 PRINT"YOU ARE AT (";X;",";Y;") LEVEL ";Z:RETURN